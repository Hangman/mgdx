apply plugin: 'application'

sourceSets.main.resources.srcDirs += [ 'assets' ]
mainClassName = 'net.mgsx.gltf.composer.GLTFComposerLauncher'
eclipse.project.name = 'gdx-gltf-composer'
sourceCompatibility = 1.8

dependencies {
	implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
	implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
	implementation project(':core')
	implementation project(':desktop')
	implementation project(':gltf')
	implementation project(':gltfx')
}

def os = System.properties['os.name'].toLowerCase()

run {
	workingDir = 'assets'
	setIgnoreExitValue(true)

	if (os.contains('mac')) {
		// Required to run LWJGL3 Java apps on MacOS
		jvmArgs += "-XstartOnFirstThread"
	}
}

jar {
	archiveBaseName.set('GLTFComposer')
	duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
	dependsOn configurations.runtimeClasspath
	from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
	exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
	dependencies {
		exclude('META-INF/INDEX.LIST', 'META-INF/maven/**')
	}
	manifest {
		attributes 'Main-Class': project.mainClassName
	}
	doLast {
		file(archiveFile).setExecutable(true, false)
	}
}

// Equivalent to the jar task; here for compatibility with gdx-setup.
task dist(dependsOn: [jar]) {
}
